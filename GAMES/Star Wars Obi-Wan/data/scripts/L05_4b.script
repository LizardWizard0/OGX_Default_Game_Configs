Copyright (c) 2000, LucasArts Entertainment Company LLC
VERSION 0.1
 
FadeUp

SetDefaultSurface 08_13fnttop.sur
loaddlg Asha.dlg

///////////////////
/// BEGIN MUSIC ///
///////////////////

AddMusic m_suspense_05.wav Suspense
AddMusic m_suspense_07.wav Suspense
AddMusic m_suspense_09.wav Suspense
AddMusic m_suspense_10.wav Suspense
AddMusic m_suspense_11.wav Suspense
AddMusic m_suspense_13.wav Suspense
AddMusic m_suspense_14.wav Suspense
AddMusic m_suspense_18.wav Suspense

AddMusic m_sighting_02.wav Tension
AddMusic m_sighting_03.wav Tension
AddMusic l03_m_intro1.wav Tension
AddMusic m_tension_02.wav Tension 
AddMusic m_tension_03.wav Tension
AddMusic m_tension_04.wav Tension
AddMusic m_tension_07.wav Tension
AddMusic m_tension_09.wav Tension
AddMusic m_tension_16.wav Tension
AddMusic l01_5_m_placard.wav Tension
AddMusic m_tension_18.wav Tension

AddMusic m_battle1.wav Battle
AddMusic l14_m_battle1.wav Battle
AddMusic m_battle2.wav Battle
AddMusic m_battle3.wav Battle
AddMusic m_battle4.wav Battle
AddMusic m_battle_07.wav Battle
AddMusic m_battle_08.wav Battle
AddMusic m_battle_11.wav Battle
AddMusic m_victory_03.wav Battle
AddMusic m_victory_04.wav Battle

AddBattleLoop  m_battle_09_lp.wav  m_battle_09_win.wav  m_battle_09_lose.wav
AddBattleLoop  m_battle_12_lp.wav  m_battle_12_win.wav  m_battle_12_lose.wav
AddBattleLoop  m_battle_13_lp.wav  m_battle_12_win.wav  m_battle_13_lose.wav
AddBattleLoop  m_saberArena_03_lp.wav  m_saberArena_03_end.wav  m_saberArena_03_lose.wav

SetMusicInterval Suspense 50 70
SetMusicInterval Tension 18 40

StartMusicManager

/////////////////
/// END MUSIC ///
/////////////////

/// GLOBAL VARS ///

int ObiStart01 0

/////////////////////
///BEGIN FUNCTIONS///
/////////////////////

/// PARTICLE GENERATOR FUNCTIONS ///

function "GenBirds" (actor Birdie, refpos gen_birdie)
{
    waituntilnearrefpos obi gen_birdie 0.5
    CreateParticleGen birds.gen Birdie gen_birdie
    pause 30.0
    loop
}

/// ENEMY FUNCTIONS ///

function "RuntoNodePartial" (actor RoadRunner, node goto_RoadRunner)
{
    takepartialcontrolof RoadRunner
    runtonodeandwait RoadRunner goto_RoadRunner
    returncontrolto RoadRunner
    addpatrolstate RoadRunner
}

function "DEDrunto" (Actor DEDhead, node goto_DEDhead)
{
	takecontrolof DEDhead
	dedroll DEDhead
	walktonodeandwait DEDhead goto_DEDhead
	dedrolloff DEDhead
	returncontrolto DEDhead
}

/// ALARM FUNCTIONS ///

function "RaiseTheAlarm" (actor Commander, actor alarm_Commander, node goto_Commander)
{
    int ObiHasNudged_Alarm 0
    
    childscript "ObiNudges"
    {
        child waituntilnudgedby alarm_Commander obi
        child waituntildamaged alarm_Commander
        waitforany
        
        set ObiHasNudged_Alarm 1
    }
    
    waituntilsees Commander Obi 10.0
    
    childscript "TerminateAlarmMode"
    {
        waituntilequal ObiHasNudged_Alarm 1
        endscript "RaiseTheRoof"
        ReturnControlTo Commander
    }

    childscript "CommanderFailsafe"
    {
        waituntilkilled Commander
        endscript "RaiseTheRoof"
    }
    
    childscript "RaiseTheRoof"
    {
        if (= ObiHasNudged_Alarm 0)
        {
            takecontrolof Commander
            runtonodeandwait Commander goto_Commander
            turntonodeorientationandwait Commander goto_Commander
            
            if (= ObiHasNudged_Alarm 0)
            {
                PlayGesture Commander typing 10
                pause 3.0
                SayLineNonPositional Commander "/BattleDroidInfantry6_12/Send reinforcements!"
                nudge Commander alarm_Commander
                ReturnControlTo Commander
            }
            else
            {
                ReturnControlTo Commander
            }
        }
    }
    waitforall
}

function "CallingAllCars" (actor BackUp, actor alarm_BackUp, node goto_BackUp, actor FloodGate)
{
    waituntilnudged alarm_BackUp
    
    script "AlarmOn"
    {
        PlayAudioCueandwait alarm_BackUp PointSoundOneshot_01
        loop
    }
    
    childscript "AlarmOff"
    {
        waituntilkilled alarm_BackUp
        endscript "AlarmOn"
    }
    childscript "BackUpActions"
    {
        clearaiboundary BackUp
    
        pause 0.5
        
        opendoor FloodGate
        takepartialcontrolof BackUp
        runtonodeandwait BackUp goto_BackUp
        returncontrolto BackUp
        addpatrolstate BackUp
    }
    waitforall
}    

/// TELEPORTER FUNCTIONS ///

function "CallingAllCars_Teleport" (actor BackUp_Teleport, actor alarm_BackUp_Teleport, node goto_BackUp_Teleport, refpos rpos_BackUp_Teleport, float breatheANDstop)
{
    waituntilnudged alarm_BackUp_Teleport
    pause breatheANDstop
    teleport BackUp_Teleport rpos_BackUp_Teleport
    pause 0.25
    takepartialcontrolof BackUp_Teleport
    runtonodeandwait BackUp_Teleport goto_BackUp_Teleport
    returncontrolto BackUp_Teleport
    addpatrolstate BackUp_Teleport
}

function "BeamMeUpAndRun" (actor dooLittle, refpos refpos_dooLittle, region RGN_dooLittle, node goto_dooLittle, float breatheANDstop)
{
    waituntilenteredregion obi RGN_dooLittle
    pause breatheANDstop
    teleport dooLittle refpos_dooLittle
    pause 0.25
    takepartialcontrolof dooLittle
    runtonodeandwait dooLittle goto_dooLittle
    returncontrolto dooLittle
    addpatrolstate dooLittle
}

function "BeamMeUpAndPatrol" (actor Ichiro, refpos refpos_Ichiro, region RGN_Ichiro, int numIchiro, float floatIchiro)
{
    waituntilenteredregion obi RGN_Ichiro
    pause floatIchiro
    teleport Ichiro refpos_Ichiro
    pause 0.25
    setpatroltypes Ichiro numIchiro
}

/////////////////////
/// END FUNCTIONS ///
/////////////////////

script "Lame"
{
    lockdoor A_5-4_door_02
}

/// SAVE POSITION SCRIPTS ///

script "Checkpoint01" 20
{
	SetStartPosition 0
    waituntilenteredregion obi RGN_Start01
    removegroup 20
    set ObiStart01 1
    SetStartPosition 1
    Save
}

script "Checkpoint02a" 21
{
    waituntilenteredregion obi RGN_Start02a
    removegroup 21
    removegroup 29
    SetStartPosition 2
    Save
}

script "Checkpoint02b" 22
{
    waituntilenteredregion obi RGN_Start02b
    removegroup 22
    removegroup 29
    SetStartPosition 3
    Save
}

script "Checkpoint02c" 23
{
    waituntilenteredregion obi RGN_Start02c
    removegroup 23
    removegroup 29
    SetStartPosition 4
    Save
}
/*
script "LoadPoint"
{
    waituntilenteredregion obi RGN_Load_L05_4a

    begincutscene
    CameraTrack camera_leveltransition Obi
    unequipweapon Obi
    walktonode Obi goto_loadA
    FadeDownandWait
    EndCutScene

    setstartposition 0

    nextpart L05_4a
}
*/

/// MISC ENVIRONMENT SCRIPTS ///

function "WaxOnWaxOff" (string waterfall, region RGN_waterfall)
{
    particlegenoff waterfall
    waituntilenteredregion obi RGN_waterfall
    particlegenon waterfall
    waituntilexitedregion obi RGN_waterfall
    loop
}

script "WaterfallOffOnSwitches"
{
    //call WaxOnWaxOff (A_waterfallMist2p4_01, RGN_WaterfallDetect01)
}

script "BirdGenerators"
{
    actor Bird01
    actor Bird02
    
    call GenBirds (Bird01, gen_birds01)
    call GenBirds (Bird02, gen_birds02)
}

script "InvincibleForceFields"
{
    invincibilityon A_5-5_ff_generator_01
}

///////////////////
///GLOBAL ACTORS///
///////////////////

nudge obi A_5-5_ff_controller_01

////////////////
///BEGIN MAIN///
////////////////

Script "Opening Conversation" 1
{
    actor Naboo01 A_Naboo-Resistance1_02
    actor Cannon13 A_BAD_cannon_13
    actor Cannon03 A_BAD_cannon_03
    
    forcetargetandattack Naboo01 Cannon13
    forcetargetandattack Cannon13 A_09_rock2_01

    child waituntildooropen A_5-4_door_03
    child waituntilequal ObiStart01 1
    waitforany
    
    childscript "Cannon13Attack"
    {
        forcetargetandattackoff Cannon13
        returncontrolto Cannon13
    }
    childscript 
    {
        forcetargetandattack Cannon13 A_dummy_object_01
        pause 3.0
        forcetargetandattackoff Cannon13
    }
    childscript "Naboo01babble"
    {
        waituntilnear Naboo01 Obi 5.0
        forcetargetandattackoff Naboo01
        takecontrolof Naboo01
        LookAt Naboo01 Obi
        SayLine Naboo01 "/male3_531/Thank goodness you're here, Jedi!" 
        stopaction Naboo01
        LookAt Naboo01 Cannon13
        playgesture Naboo01 PointForward 3.0
        SayLine Naboo01 "/male3_672/We're having a little trouble with that repeater cannon.  It's got us pinned down."
        SayLine Naboo01 "/male3_668/If only we could get around behind it."
        stopaction Naboo01
        returncontrolto Naboo01
    }
    waitforall
}

Script "ResistanceThanksObi" 1
{
	Actor Naboo02 A_Naboo-Resistance3_02
	Actor Naboo03 A_Naboo-Resistance1_01

    child waituntildooropen A_5-4_door_03
    child waituntilequal ObiStart01 1
    waitforany
/*    
    childscript "ReturnToNormal"
    {
        forcetarget Naboo02 A_BAD_cannon_03
        forcetarget Naboo03 A_BAD_cannon_13
        
        pause 2.0        
        
        forcetargetoff Naboo02
        forcetargetoff Naboo03
    }
*/    
    childscript "VictoryDance"
    {
        child waituntilkilled A_BAD_cannon_03
        child waituntilkilled A_BAD_cannon_13
        waitforall

        PlayFlourish m_victory_04.wav

        childscript "Naboo02thanks"
        {
            waituntilnear Obi Naboo02 7.0
            TakeControlOf Naboo02
            LookAt Naboo02 Obi
            sayline Naboo02 "/male1_445/Thanks for saving us, Jedi!"
            sayline Naboo02 "/male1_655/There's a big dish nearby, a communications satellite, I think" 
            stopaction Naboo02
            Returncontrolto Naboo02
            followstate Naboo02 Obi 5.0
        }
        
        childscript "Naboo03thanks"
        {
            waituntilnear Obi Naboo03 7.0
            TakeControlOf Naboo03
            LookAt Naboo03 Obi
            sayline Naboo03 "/male1_445/Thanks for saving us, Jedi!"
            sayline Naboo03 "/male1_655/There's a big dish nearby, a communications satellite, I think" 
            stopaction Naboo03
            Returncontrolto Naboo03
            followstate Naboo03 Obi 5.0
        }
        waitforall
    }
	waitForAll
}

script "Group04_Enemies" 4
{
    setpatroltypes A_Battledroid-Security_19 1        /// BAD BY THE STEALTH BOXES ///
    
    script "BADattack_Intercept01"
    {
        childscript "Option1"
        {
            waituntilenteredregion obi RGN_BAD11
            call RuntonodePartial (A_Battledroid-Infantry_19, goto_intercept01a)
            call RuntonodePartial (A_Battledroid-Infantry_26, goto_intercept01a)
        }
        childscript "Option2"
        {
            waituntilenteredregion obi RGN_BAD54
            call RuntonodePartial (A_Battledroid-Infantry_19, goto_intercept01b)
            call RuntonodePartial (A_Battledroid-Infantry_26, goto_intercept01b)
        }
        waitforany
    }
    
    script "BADattack_CannonIntercept"
    {
        child waituntilenteredregion obi RGN_BAD54
        child waituntilenteredregion obi RGN_BAD116
        waitforany
        
        call RuntonodePartial (A_Battledroid-Infantry_30, goto_intercept01b)
        call RuntonodePartial (A_Battledroid-Infantry_31, goto_intercept01b)
        waitforall
    }
}
    
script "CommanderAlarm01" 5
{
    setpatroltypes A_Battledroid-Commander_02 3
    call RaiseTheAlarm (A_Battledroid-Commander_02, A_5-3_alarm_01, goto_alarm01)
}
    
script "RespondToAlarm01" 6
{  
    //call CallingAllCars (A_barondroid_02, A_5-3_alarm_01, goto_alarm01, A_5-4_doorlk_03)
    call CallingAllCars (A_Battledroid-Security_28, A_5-3_alarm_01, goto_alarm01, A_5-4_doorlk_03)
    call CallingAllCars (A_Battledroid-Security_31, A_5-3_alarm_01, goto_alarm01, A_5-4_doorlk_03)
}

script "BADattack_Alarm01_AlwaysBackUp" 7
{
    //call CallingAllCars_Teleport (A_Battledroid-Infantry_07, A_5-3_alarm_01, goto_alarm01, rpos_bad31, 0)
    //call CallingAllCars_Teleport (A_Battledroid-Infantry_32, A_5-3_alarm_01, goto_alarm01, rpos_bad31, 0.1)
    //call CallingAllCars_Teleport (A_Battledroid-Infantry_33, A_5-3_alarm_01, goto_alarm01, rpos_bad31, 0.2)

    call BeamMeUpAndRun (A_Battledroid-Infantry_07, rpos_bad31, RGN_BAD31, goto_alarm01, 0)
    call BeamMeUpAndRun (A_Battledroid-Infantry_32, rpos_bad32, RGN_BAD31, goto_alarm01, 0.25)
    call BeamMeUpAndRun (A_Battledroid-Infantry_33, rpos_bad33, RGN_BAD31, goto_alarm01, 0.5)
}

script "Group08_Enemies" 8
{
    setpatroltypes A_Battledroid-Sniper_03 1          /// BAD BY ALARM01 ///
}

/// BADS ABOVE THE ROTUNDA ///

script "BADattack_AboveTheRotunda" 9
{
    call BeamMeUpAndPatrol (A_Battledroid-Security_24, rpos_security24, RGN_BAD61, 2, 0)
    call BeamMeUpAndPatrol (A_Battledroid-Security_25, rpos_security25, RGN_BAD61, 3, 0.25)

    waituntilenteredregion obi RGN_BAD81

    call RuntonodePartial (A_Battledroid-Infantry_27, ND_BAD81)
    call RuntonodePartial (A_Battledroid-Infantry_28, ND_BAD82)
    call RuntonodePartial (A_Battledroid-Infantry_29, ND_BAD83)
}

script "CommanderAlarm03" 10
{
    setpatroltypes A_Battledroid-Commander_06 1
    call RaiseTheAlarm (A_Battledroid-Commander_06, A_5-3_alarm_03, goto_alarm03)
}

script "RespondToAlarm03" 11
{  
    //call CallingAllCars (A_barondroid_01, A_5-3_alarm_03, goto_alarm03, A_5-4_doorlk_01)
    call CallingAllCars (A_Battledroid-Security_26, A_5-3_alarm_03, goto_alarm03, A_5-4_doorlk_01)
    call CallingAllCars (A_Battledroid-Security_27, A_5-3_alarm_03, goto_alarm03, A_5-4_doorlk_01)
}

script "Group12_Enemies" 12
{
    setpatroltypes A_Battledroid-Sniper_04 1    /// SNIPER ON THE BALCONY ABOVE THE ROTUNDA ///
}

/////////////////////////////////
/// BADS IN THE INNER SANCTUM ///
/////////////////////////////////

/// FIRST TIER BADS ///

script "Group13_Enemies" 13
{
    script "BADsByTheDoor01"
    {
        setpatroltypes A_Battledroid-Infantry_40 2
        setpatroltypes A_Battledroid-Infantry_48 2
        setpatroltypes A_Battledroid-Security_22 2
        
        setpatroltypes A_Battledroid-Security_23 2
    }
    
    script "BADpatrolAboveDoor01"
    {
        setpatroltypes A_Battledroid-Infantry_55 1
        setpatroltypes A_Battledroid-Infantry_06 1
        setpatroltypes A_Battledroid-Infantry_08 1
        setpatroltypes A_Battledroid-Infantry_09 1
    }    
}

script "CommanderAlarm02" 14
{
    setpatroltypes A_Battledroid-Commander_05 1
    call RaiseTheAlarm (A_Battledroid-Commander_05, A_5-3_alarm_02, goto_alarm02)
}

script "RespondToAlarm02a" 15
{  
    //call CallingAllCars (A_barondroid_03, A_5-3_alarm_02, goto_alarm02, A_5-4_doorlk_04)
    call CallingAllCars (A_Battledroid-Security_30, A_5-3_alarm_02, goto_alarm02, A_5-4_doorlk_04)
    call CallingAllCars (A_Battledroid-Security_29, A_5-3_alarm_02, goto_alarm02, A_5-4_doorlk_04)
}

script "RespondToAlarm02b" 16
{  
    //call CallingAllCars (A_barondroid_04, A_5-3_alarm_02, goto_alarm02, A_5-4_doorlk_02)
    call CallingAllCars (A_Battledroid-Security_41, A_5-3_alarm_02, goto_alarm02, A_5-4_doorlk_02)
    call CallingAllCars (A_Battledroid-Security_42, A_5-3_alarm_02, goto_alarm02, A_5-4_doorlk_02)
}

script "Group17_Enemies" 17
{
    /// SECOND TIER BADS ///
    
    setpatroltypes A_Battledroid-Infantry_49 2
    setpatroltypes A_Battledroid-Infantry_39 2
    setpatroltypes A_Battledroid-Infantry_50 2
    setpatroltypes A_Battledroid-Security_36 2
    setpatroltypes A_Battledroid-Commander_04 2
    
    /// THIRD TIER BADS ///
    
    setpatroltypes A_Battledroid-Infantry_51 1
    setpatroltypes A_Battledroid-Infantry_52 1
    setpatroltypes A_Battledroid-Infantry_53 1
    setpatroltypes A_Battledroid-Infantry_54 1
    //setpatroltypes A_barondroid_07 2
}

script "Group_Enemies18" 18
{
    /// BADS GUARDING DOOR TO FINAL COURTYARD ///
    
    setpatroltypes A_Battledroid-Security_34 2
    setpatroltypes A_Battledroid-Security_35 2
    
    /// BADS BY CANNON ///
    
    setpatroltypes A_Battledroid-Security_43 3
    setpatroltypes A_Battledroid-Security_44 3
}

/// BAD IN THE INNER INNER SANCTUM ///

script "Group24_Enemies" 24
{
    setpatroltypes A_Battledroid-Security_40 1
}

/////////////////////////////
/// BEGIN FINAL COURTYARD ///
/////////////////////////////

addpatrolstate A_Battledroid-Security_02
addpatrolstate A_Battledroid-Security_37
addpatrolstate A_Battledroid-Security_38
addpatrolstate A_Battledroid-Security_39

script "DED1_Encounter"
{
	Actor DED1 A_DestroyerDroid_01

    childscript "MainEntrance"
    {
        waituntildooropen A_5-4_door_09
        //triggerubergen A_UG_Zone57_STAP01
        call DEDrunto (DED1, goto_DED1)
    }
    childscript "CannonEntrance"
    {
        waituntildooropen A_5-4_doorlk_05
        setpatroltypes DED1 2
    }
    childscript "SniperEntrance"
    {
        waituntilenteredregion Obi RGN_Start02c
        setpatroltypes DED1 2
    } 
    waitforany
}

script "DEDs_GuardingGun"
{
	Actor DED2 A_DestroyerDroid_02
}

script "SaveYourBuddy"
{
    actor Naboo04 A_Naboo-Resistance3_01

    setteam Naboo04 0

    child waituntildooropen A_5-4_doorlk_05
    child waituntilsees Obi Naboo04 20.0
    waitforany
    
    setteam Naboo04 5
    
    waituntilkilled A_BAD_cannon_06
    
    takepartialcontrolof Naboo04
    runtonodeandwait Naboo04 goto_cannon06
    returncontrolto Naboo04

    forcetargetandattack Naboo04 A_DestroyerDroid_01
    waituntilkilled A_DestroyerDroid_01
    forcetargetandattackoff Naboo04

    followstate Naboo04 Obi 5.0
}

setpatroltypes A_Battledroid-Sniper_01 1

/// FIRST TIER ///

setpatroltypes A_DestroyerDroid_05 1

/// SECOND TIER ///

setpatroltypes A_Battledroid-Security_11 2
setpatroltypes A_Battledroid-Security_12 2

setpatroltypes A_DestroyerDroid_04 3

setpatroltypes A_Battledroid-Sniper_05 1

script "CannonFodder_North"
{
    call BeamMeUpAndRun (A_Battledroid-Security_46, teleport_BAD01, RGN_SnipeCannon01, goto_snipecannon01, 0)
    call BeamMeUpAndRun (A_Battledroid-Security_47, teleport_BAD02, RGN_SnipeCannon01, goto_snipecannon01, 0.1)
    call BeamMeUpAndRun (A_Battledroid-Security_48, teleport_BAD03, RGN_SnipeCannon01, goto_snipecannon01, 0.2)
    call BeamMeUpAndRun (A_Battledroid-Security_49, teleport_BAD04, RGN_SnipeCannon01, goto_snipecannon01, 0.3)
    call BeamMeUpAndRun (A_Battledroid-Security_50, teleport_BAD05, RGN_SnipeCannon01, goto_snipecannon01, 0.4)
}

script "CannonFodder_West"
{
    call BeamMeUpAndRun (A_Battledroid-Security_51, teleport_BAD06, RGN_SnipeCannon02, goto_snipecannon02, 0)
    call BeamMeUpAndRun (A_Battledroid-Security_52, teleport_BAD07, RGN_SnipeCannon02, goto_snipecannon02, 0.1)
    call BeamMeUpAndRun (A_Battledroid-Security_53, teleport_BAD08, RGN_SnipeCannon02, goto_snipecannon02, 0.2)
    call BeamMeUpAndRun (A_Battledroid-Security_54, teleport_BAD09, RGN_SnipeCannon02, goto_snipecannon02, 0.3)
    call BeamMeUpAndRun (A_Battledroid-Security_55, teleport_BAD10, RGN_SnipeCannon02, goto_snipecannon02, 0.4)
}

Script "BlowUpGun" 
{
    /// BEGIN TURBOLASER FIRE ///
    
    getattachment A_5-4_turbolaser1_01 TurboLaser
      
    waituntilnear obi TurboLaser 120.0
    
    script "AnimationLoop"
    {
        playanimation TurboLaser 5-4_turbolaser2.anim
	pause 0.1
	fireblasterbolt TurboLaser turbolaser_upper.pjl emitter04-ROOT
	pause 0.1
	fireblasterbolt TurboLaser turbolaser_upper.pjl emitter05-ROOT
	pause 0.1
	fireblasterbolt TurboLaser turbolaser.pjl emitter03-ROOT
	pause 0.1
	fireblasterbolt TurboLaser turbolaser.pjl emitter06-ROOT
	pause 2
        loop
    }

    /// END TURBOLASER FIRE ///

    Actor BigGun A_5-4_turbolaser1_01

	Actor SensitiveGunSpot01 A_5-4_turbolaser_leg_01
    Actor SensitiveGunSpot02 A_5-4_turbolaser_leg_02
    
    InvincibilityOn BigGun
    
    child WaitUntilKilled SensitiveGunSpot01
    child WaitUntilKilled SensitiveGunSpot02
    waitforall
    
    MissionObjectiveAchieved 1
    
    takecontrolof A_DestroyerDroid_02
    takecontrolof A_DestroyerDroid_03
    
	BeginCutScene

	StopMusicManager
    playflourish cut_5_4bextro_m.WAV

    invincibilityon Obi
    InvincibilityOff BigGun

    removeactor A_DestroyerDroid_02
    removeactor A_DestroyerDroid_03

    /// EXPLOSION FRAME ///

    camerafixed camera_explosion
    
    childscript "PowerDown"
    {
        pause 3.5
        
        // endscript "FireBottomLeftLoop"
        // pause 0.5
        // endscript "FireBottomRightLoop"

        // pause 1.0
        
        // endscript "FireTopLeftLoop"
        // pause 0.5
        // endscript "FireTopRightLoop"
        endscript "AnimationLoop"
    }

    waitforall    

    teleport obi teleport_obi_endcutscene
    
    pause 1.5

    /// OBI TAKES A DIVE ///
    
    cameratrack camera_end obi

    unequipweapon Obi
    
    runtonode obi goto_obi_theend

    waituntilnearnode obi goto_obi_jumpforjoy 1.0
    
    pause 0.25   
    
    child Kill BigGun
    child PlayFlourish aaGun_explo_01.wav
    
    childscript "EndDialogue"
    {
        CommunicatorOn
        CommunicatorMessageAndWait "/Asha_674/Great job, Jedi!" ResistanceLeader.dlg
        CommunicatorMessageAndWait "/Asha_675/The turbolaser just disappeared from our screens.  It's been destroyed." ResistanceLeader.dlg
        CommunicatorOff
    }
    
    waitforall
	
	FadeDownAndWait
	EndCutScene

    NextLevel L05_6
}

///////////////////////////
/// END FINAL COURTYARD ///
///////////////////////////

//////////////////////////////
/// BEGIN GROUPING SCRIPTS ///
//////////////////////////////

function "ExplorationMarkerRemoval" (actor ExplorationMarker, int ExplorationMarkerGroup)
{
    waituntilremoved ExplorationMarker
    removegroup ExplorationMarkerGroup
}

call ExplorationMarkerRemoval (A_powerupexplorationmarker_01, 32)
call ExplorationMarkerRemoval (A_powerupexplorationmarker_02, 31)
call ExplorationMarkerRemoval (A_powerupexplorationmarker_03, 30)

/// ENEMY COUNT ///

script "GroupCount01" 1
{
    int NumKilled_Group01 0
    
    function "RemoveGroupCount01" (actor Groupie01)
    {
        waituntilkilled Groupie01
        increment NumKilled_Group01 1
    }
    
    call RemoveGroupCount01 (A_Naboo-Resistance1_01)
    call RemoveGroupCount01 (A_Naboo-Resistance1_02)
    call RemoveGroupCount01 (A_Naboo-Resistance3_02)
    
    waituntilequal NumKilled_Group01 2
    removegroup 1
}

script "GroupCount04" 4
{
    int NumKilled_Group04 0
    
    function "RemoveGroupCount04" (actor Groupie04)
    {
        waituntilkilled Groupie04
        increment NumKilled_Group04 1
    }
    
    call RemoveGroupCount04 (A_Battledroid-Security_19)
    call RemoveGroupCount04 (A_Battledroid-Infantry_19)
    call RemoveGroupCount04 (A_Battledroid-Infantry_26)
    call RemoveGroupCount04 (A_Battledroid-Infantry_30)
    call RemoveGroupCount04 (A_Battledroid-Infantry_31)
    
    waituntilequal NumKilled_Group04 3
    removegroup 4
}

script "GroupCount05" 5
{
    waituntilkilled A_Battledroid-Commander_02
    removegroup 5
}

/*
script "GroupCount06" 6
{
    int NumKilled_Group06 0
    
    function "RemoveGroupCount06" (actor Groupie06)
    {
        waituntilkilled Groupie06
        increment NumKilled_Group06 1
    }
    
    call RemoveGroupCount06 (A_barondroid_02)
    call RemoveGroupCount06 (A_Battledroid-Security_28)
    call RemoveGroupCount06 (A_Battledroid-Security_31)
    
    waituntilequal NumKilled_Group06 2
    removegroup 6
}
*/

script "GroupCount07" 7
{
    int NumKilled_Group07 0
    
    function "RemoveGroupCount07" (actor Groupie07)
    {
        waituntilkilled Groupie07
        increment NumKilled_Group07 1
    }
    
    call RemoveGroupCount07 (A_Battledroid-Infantry_07)
    call RemoveGroupCount07 (A_Battledroid-Infantry_32)
    call RemoveGroupCount07 (A_Battledroid-Infantry_33)
    
    waituntilequal NumKilled_Group07 2
    removegroup 7
}

script "GroupCount8" 8
{
    waituntilkilled A_Battledroid-Sniper_03
    removegroup 8
}

script "GroupCount09" 9
{
    int NumKilled_Group09 0
    
    function "RemoveGroupCount09" (actor Groupie09)
    {
        waituntilkilled Groupie09
        increment NumKilled_Group09 1
    }
    
    call RemoveGroupCount09 (A_Battledroid-Security_24)
    call RemoveGroupCount09 (A_Battledroid-Security_25)
    call RemoveGroupCount09 (A_Battledroid-Infantry_27)
    call RemoveGroupCount09 (A_Battledroid-Infantry_28)
    call RemoveGroupCount09 (A_Battledroid-Infantry_29)

    waituntilequal NumKilled_Group09 2
    removegroup 9
}

script "GroupCount10" 10
{
    waituntilkilled A_Battledroid-Commander_06
    removegroup 10
}

/*
script "GroupCount11" 11
{
    int NumKilled_Group11 0
    
    function "RemoveGroupCount11" (actor Groupie11)
    {
        waituntilkilled Groupie11
        increment NumKilled_Group11 1
    }
    
    call RemoveGroupCount11 (A_barondroid_01)
    call RemoveGroupCount11 (A_Battledroid-Security_26)
    call RemoveGroupCount11 (A_Battledroid-Security_27)
    
    waituntilequal NumKilled_Group11 2
    removegroup 11
}
*/

script "GroupCount12" 12
{
    waituntilkilled A_Battledroid-Sniper_04
    removegroup 12
}

script "GroupCount13" 13
{
    int NumKilled_Group13 0
    
    function "RemoveGroupCount13" (actor Groupie13)
    {
        waituntilkilled Groupie13
        increment NumKilled_Group13 1
    }
    
    call RemoveGroupCount13 (A_Battledroid-Infantry_40)
    call RemoveGroupCount13 (A_Battledroid-Infantry_48)
    call RemoveGroupCount13 (A_Battledroid-Infantry_55)
    call RemoveGroupCount13 (A_Battledroid-Infantry_06)
    call RemoveGroupCount13 (A_Battledroid-Infantry_08)
    call RemoveGroupCount13 (A_Battledroid-Infantry_09)
    call RemoveGroupCount13 (A_Battledroid-Security_22)
    call RemoveGroupCount13 (A_Battledroid-Security_23)
    
    waituntilequal NumKilled_Group13 5
    removegroup 13
}

script "GroupCount14" 14
{
    waituntilkilled A_Battledroid-Commander_05
    removegroup 14
}

/*
script "GroupCount15" 15
{
    int NumKilled_Group15 0
    
    function "RemoveGroupCount15" (actor Groupie15)
    {
        waituntilkilled Groupie15
        increment NumKilled_Group15 1
    }
    
    call RemoveGroupCount15 (A_barondroid_03)
    call RemoveGroupCount15 (A_Battledroid-Security_30)
    call RemoveGroupCount15 (A_Battledroid-Security_29)
    
    waituntilequal NumKilled_Group15 2
    removegroup 15
}

script "GroupCount16" 16
{
    int NumKilled_Group16 0
    
    function "RemoveGroupCount16" (actor Groupie16)
    {
        waituntilkilled Groupie16
        increment NumKilled_Group16 1
    }
    
    call RemoveGroupCount16 (A_barondroid_04)
    call RemoveGroupCount16 (A_Battledroid-Security_41)
    call RemoveGroupCount16 (A_Battledroid-Security_42)
    
    waituntilequal NumKilled_Group16 2
    removegroup 16
}
*/

script "GroupCount17" 17
{
    int NumKilled_Group17 0
    
    function "RemoveGroupCount17" (actor Groupie17)
    {
        waituntilkilled Groupie17
        increment NumKilled_Group17 1
    }
    
    call RemoveGroupCount17 (A_Battledroid-Commander_04)
    call RemoveGroupCount17 (A_Battledroid-Infantry_49)
    call RemoveGroupCount17 (A_Battledroid-Infantry_39)
    call RemoveGroupCount17 (A_Battledroid-Infantry_50)
    call RemoveGroupCount17 (A_Battledroid-Infantry_51)
    call RemoveGroupCount17 (A_Battledroid-Infantry_52)
    call RemoveGroupCount17 (A_Battledroid-Infantry_53)
    call RemoveGroupCount17 (A_Battledroid-Infantry_54)
    call RemoveGroupCount17 (A_Battledroid-Security_36)
    //call RemoveGroupCount17 (A_barondroid_07)
    waituntilequal NumKilled_Group17 5
    removegroup 17
}

script "GroupCount18" 18
{
    int NumKilled_Group18 0
    
    function "RemoveGroupCount18" (actor Groupie18)
    {
        waituntilkilled Groupie18
        increment NumKilled_Group18 1
    }
    
    call RemoveGroupCount18 (A_Battledroid-Security_34)
    call RemoveGroupCount18 (A_Battledroid-Security_35)
    call RemoveGroupCount18 (A_Battledroid-Security_43)
    call RemoveGroupCount18 (A_Battledroid-Security_44)
    
    waituntilequal NumKilled_Group18 2
    removegroup 18
}

script "GroupCount24" 24
{
    waituntilkilled A_Battledroid-Security_40
    removegroup 24
}

script "GroupCount25" 25
{
    waituntilkilled A_BAD_cannon_13
    removegroup 25
}

script "GroupCount26" 26
{
    waituntilkilled A_BAD_cannon_03
    removegroup 26
}

script "GroupCount27" 27
{
    waituntilkilled A_BAD_cannon_04
    removegroup 27
}

script "GroupCount28" 28
{
    waituntilkilled A_BAD_cannon_05
    removegroup 28
}

////////////////////////////
/// END GROUPING SCRIPTS ///
////////////////////////////